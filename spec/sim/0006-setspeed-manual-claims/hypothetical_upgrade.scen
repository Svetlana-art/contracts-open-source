#!/usr/bin/env yarn repl -s

-- This script tests a hypothetical upgrade with manual BENQI claims and manual BENQI speed setting

PrintTransactionLogs
Alias QiHolder "0xC89b6f0146642688bb254bF93C28fcCF1E182C81"
Alias USDCWhale "0x3dfd23a6c5e8bbcfc9581d2e864a68feb6a076d3"
Alias DAIWhale "0x47ac0Fb4F2D84898e4D9E7b4DaB3C24507a6D503"
Web3Fork "https://mainnet-eth.benqi.finance/@11458477" (QiHolder USDCWhale DAIWhale)
UseConfigs mainnet

-- Deploy the flywheel impl
ComptrollerImpl Deploy Standard ComptrollerG7

-- Baseline USDC benqi speed before the patch
Assert Equal (Comptroller QiSpeed qiUSDC) 5.3786477206671197e16

-- Mint tokens
From DAIWhale (Trx GasPrice 0 (Erc20 DAI Approve qiDAI UInt256Max))
From DAIWhale (Trx GasPrice 0 (QiToken qiDAI Mint 10000e6))
From USDCWhale (Trx GasPrice 0 (Erc20 USDC Approve qiUSDC UInt256Max))
From USDCWhale (Trx GasPrice 0 (QiToken qiUSDC Mint 10000e6))

-- Baseline BENQI claim speed
Assert Equal (Comptroller CheckIsQied qiDAI) True

Comptroller ClaimQi DAIWhale
AdvanceBlocks 1000
Expect Changes (Erc20 Benqi TokenBalance DAIWhale) 7.77e2
Comptroller ClaimQi DAIWhale

-- Propose to apply the patch

From QiHolder (Benqi Delegate QiHolder)
From QiHolder (Governor GovernorAlpha Propose "Disable automatic benqi speed refresh and automatic claims and change benqi speed" [(Address Unitroller) (Address ComptrollerG7) (Address Unitroller)] [0 0 0] ["_setPendingImplementation(address)" "_become(address)" "_setQiSpeed(address,uint256)"] [[(Address ComptrollerG7)] [(Address Unitroller)] [(Address qiUSDC) 30000000000000000]])

-- Vote for, queue, and execute the proposal

MineBlock
From QiHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute
ComptrollerImpl ComptrollerG7 MergeABI

-- Check that speed has changed
Assert Equal (Comptroller QiSpeed qiUSDC) 3.0e16

-- Check that BENQI is still accruing at the same speed
Comptroller ClaimQi DAIWhale
AdvanceBlocks 1000
Expect Changes (Erc20 Benqi TokenBalance DAIWhale) 7.77e2
Comptroller ClaimQi DAIWhale

-- Check that minting some USDC wont result in any automatic claims
AdvanceBlocks 1000000
From USDCWhale (Trx GasPrice 0 (Erc20 USDC Approve qiUSDC UInt256Max))
Expect Changes (Erc20 Benqi TokenBalance USDCWhale) 0
From USDCWhale (Trx GasPrice 0 (QiToken qiUSDC Mint 10000e6))

-- Prepare second proposal
From QiHolder (Governor GovernorAlpha Propose "Remove USDC market by setting benqi speed to 0" [(Address Unitroller)] [0] ["_setQiSpeed(address,uint256)"] [[(Address qiUSDC) 0]])

-- Vote for, queue, and execute the second proposal

MineBlock
From QiHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute

AdvanceBlocks 1000

-- Check speed is now 0
Assert Equal (Comptroller QiSpeed qiUSDC) 0

-- True up balances
Comptroller ClaimQi USDCWhale

-- Check no more benqi accrued
AdvanceBlocks 1000000
Expect Changes (Erc20 Benqi TokenBalance USDCWhale) 0
Comptroller ClaimQi USDCWhale

-- Prepare final proposal
From QiHolder (Governor GovernorAlpha Propose "Reset USDC benqi speed" [(Address Unitroller)] [0] ["_setQiSpeed(address,uint256)"] [[(Address qiUSDC) 27000000000000000]])

-- Vote for, queue, and execute the final proposal

MineBlock
From QiHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute

-- Check new speed
Assert Equal (Comptroller QiSpeed qiUSDC) 2.7e16

Print "Upgrade OK!"
